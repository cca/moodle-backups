#!/usr/bin/env fish

set -g TESTS 0
set -g PASSED 0
set -g FAILED 0
function fail
    set -g TESTS (math 1 + $TESTS)
    set -g FAILED (math 1 + $FAILED)
    set_color red
    echo -e "❌ test failed\n"
    set_color normal
end
function pass
    set -g TESTS (math 1 + $TESTS)
    set -g PASSED (math 1 + $PASSED)
    set_color green
    echo -e "✓ test passed\n"
    set_color normal
end
function check_result
    if test $status -eq 0
        pass
    else
        fail
    end
end
source ./lib/k8s.fish
# run tests against the staging cluster
set -gx NS moo-stg1
set -gx POD (get_pod)
set BACKUPS_PATH /bitnami/moodledata/backups
set MOODLE_PATH /bitnami/moodle

# create backup
function create
    set_color --bold
    echo -e "\tCreate a backup file on the Moodle pod...\n"
    set_color normal

    # requires a test course in staging, use TEST_COURSE env var
    # or Eric's hard-coded one here https://moodle-stg-1.cca.edu/course/view.php?id=3606
    set -q TEST_COURSE || set TEST_COURSE 3606

    ./backup.fish mk $TEST_COURSE
    kubectl exec -n$NS $POD -- ls $BACKUPS_PATH | grep backup_$TEST_COURSE 2&>/dev/null
    check_result

    kubectl exec -n$NS $POD -- sh -c 'rm /bitnami/moodledata/backups/backup_3606*'
end

# download backup
function download
    set_color --bold
    echo -e "\tDownload one file at a time from Moodle...\n"
    set_color normal

    # create fake backup files
    set file1 backup_2666_ANIMA-1001-01-2666TE_2666.06.03.mbz
    set file2 backup_2667_WRLIT-4444-04-2666TE_2666.06.03.mbz
    for file in $file1 $file2
        kubectl exec -n$NS $POD -- touch $BACKUPS_PATH/$file
    end

    ./backup.fish dl $file1
    ls data | grep $file1 >/dev/null
    check_result
    rm data/$file1

    set_color --bold
    echo -e "\tDownload the entire backups folder from Moodle...\n"
    set_color normal

    ./backup.fish dl --all
    ls data | grep $file1 >/dev/null; and ls data | grep $file2 >/dev/null
    check_result

    for file in $file1 $file2
        kubectl exec -n$NS $POD -- rm $BACKUPS_PATH/$file
        rm data/$file
    end
end

# copy backup to GSB
function copy
    set_color --bold
    echo -e "\tCopy a backup file to GSB...\n"
    set_color normal

    set FILE backup_2666_TESTS-101-01-2666TE_2022.06.21.mbz
    touch test/$FILE
    set SEMESTER 2666TE

    ./backup.fish cp $SEMESTER ./test/$FILE
    gsutil ls gs://moodle-course-archive/$SEMESTER/$FILE >/dev/null
    check_result

    gsutil rm gs://moodle-course-archive/$SEMESTER/$FILE 2&>/dev/null
end

# delete backup & course from pod
function delete
    # create a test course
    kubectl exec -n$NS $POD -- moosh --no-user-check --moodle-path $MOODLE_PATH course-create TESTS-1001-01
    # need to get the course ID from output of above cmd
    # create a backup of the test course
    kubectl exec -n$NS $POD -- moosh --no-user-check --moodle-path $MOODLE_PATH course-backup --path $BACKUPS_PATH $course
    # delete them both
    ./backup.fish rm $course
    check_result
end

echo "It should..."
# make individual tests runnable
switch $argv[1]
    case ''
        create
        download
        copy
        delete
    case create
        create
    case download
        download
    case copy
        copy
    case delete
        delete
end

# summarize results
set_color --bold
echo -n "$PASSED/$TESTS tests passed."
if test $FAILED -gt 0
    set_color red
    echo " $FAILED failure(s) - see results above."
else
    # trailing newline
    echo
end
set_color normal
